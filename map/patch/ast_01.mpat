@ $Script_Main {
    Set   *GB_WorldLocation  .Location:GoombaRoad
    Call  SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
    Call  SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
    Call  SetCamEnabled      ( .Cam:Default .True
    Call  SetCamLeadPlayer   ( .Cam:Default .False )
    Exec  $Script_SetupMusic
    Exec  $Script_ShipBindsToMario
    Exec  $Script_BindObstacles
    Exec  $Script_EnterMap
    Set *MF_SpacePerfectBonus .True
    Set *MF_SpaceFailState .False
    Exec  $Script_Main_Callback_AfterEnterMap
    Return
    End
}

#new:Script $Script_ShipBindsToMario
{
    Thread
        If *MapFlag[0] == .True
            Return
        EndIf
        Set *MapFlag[0] .True
        Call EnableGroup ( ~Model:Ships .False )
        Switch *MB_CurrentShip
        Case == 2`
            Call EnableGroup ( ~Model:Rocket2 .True )
            Loop
                Call  GetPlayerPos ( *Var0 *Var1 *Var2 )
                Call  TranslateGroup ( ~Model:Rocket2 *Var0 00000000 *Var2 )
                Wait  1`
            EndLoop
        Default
            Call EnableGroup ( ~Model:Rocket1 .True )
            Loop
                Call  GetPlayerPos ( *Var0 *Var1 *Var2 )
                Call  TranslateGroup ( ~Model:Rocket1 *Var0 00000000 *Var2 )
                Wait  1`
            EndLoop
        EndSwitch
    EndThread
    Return
    End
}

#new:Script $Script_BindObstacles
{
    Bind $Script_WeaponAttack .Trigger:FloorPressA ~Collider:FloorCollider 00000001 00000000
    Bind $Script_BulletBlowUp1 .Trigger:FloorAbove ~Collider:BulletCollider1 00000001 00000000
    Call ParentColliderToModel ( ~Collider:BulletCollider1 ~Model:Bullet1 )
    Bind $Script_BulletBlowUp2 .Trigger:FloorAbove ~Collider:BulletCollider2 00000001 00000000
    Call ParentColliderToModel ( ~Collider:BulletCollider2 ~Model:Bullet2 )
    Bind $Script_BulletBlowUp3 .Trigger:FloorAbove ~Collider:BulletCollider3 00000001 00000000
    Call ParentColliderToModel ( ~Collider:BulletCollider3 ~Model:Bullet3 )
    Call EnableGroup ( ~Model:Bullets .False )
    Call  ModifyColliderFlags   ( 00000000 ~Collider:BulletCollider1 7FFFFE00 )
    Call  ModifyColliderFlags   ( 00000000 ~Collider:BulletCollider2 7FFFFE00 )
    Call  ModifyColliderFlags   ( 00000000 ~Collider:BulletCollider3 7FFFFE00 )
    Call ParentColliderToModel ( ~Collider:Enemy1Collider ~Model:Enemy1 )
    Bind $Script_Enemy1Ram .Trigger:FloorAbove ~Collider:Enemy1Collider 00000001 00000000
    Return
    End
}

@ $Script_Main_Callback_AfterEnterMap {
    Set *MapVar[1] 20` % Ship HP
    Exec $Script_EnemyDamage
    Wait 20`
    Call MakeLerp ( 0` 1500` 120` .Easing:CosInOut )
        Loop
            Call  UpdateLerp ( )
            Call  TranslateModel ( ~Model:Enemy1 00000000 00000000 *Var0 )
            Wait  1`
            If  *Var1  ==  00000000
                BreakLoop
            EndIf
        EndLoop
    Wait 30`
    Exec $Script_FiringSequence
        Call MakeLerp ( 0` 250` 90` .Easing:CosInOut )
            Loop
                Call  UpdateLerp ( )
                Call  TranslateModel ( ~Model:Enemy1 *Var0 00000000 1500` )
                Wait  1`
                If  *Var1  ==  00000000
                    BreakLoop
                EndIf
            EndLoop
    Loop 4`
        Wait 1`
        Call MakeLerp ( 250` -250` 180` .Easing:CosInOut )
            Loop
                Call  UpdateLerp ( )
                Call  TranslateModel ( ~Model:Enemy1 *Var0 00000000 1500` )
                Wait  1`
                If  *Var1  ==  00000000
                    BreakLoop
                EndIf
                If  *MapVar[1]  <=  00000000
                    Goto 0
                EndIf
            EndLoop
            Wait 1`
        Call MakeLerp ( -250` 250` 180` .Easing:CosInOut )
            Loop
                Call  UpdateLerp ( )
                Call  TranslateModel ( ~Model:Enemy1 *Var0 00000000 1500` )
                Wait  1`
                If  *Var1  ==  00000000
                    BreakLoop
                EndIf
                If  *MapVar[1]  <=  00000000
                    Goto 0
                EndIf
            EndLoop
    EndLoop
    Call MakeLerp ( 250` 0` 90` .Easing:CosInOut )
            Loop
                Call  UpdateLerp ( )
                Call  TranslateModel ( ~Model:Enemy1 *Var0 00000000 1500` )
                Wait  1`
                If  *Var1  ==  00000000
                    BreakLoop
                EndIf
                If  *MapVar[1]  <=  00000000
                    Goto 0
                EndIf
            EndLoop
            Wait 1`
            Set *MapFlag[3] .True
        Call MakeLerp ( 1500` 0` 120` .Easing:CosInOut )
            Loop
                Call  UpdateLerp ( )
                Call  TranslateModel ( ~Model:Enemy1 00000000 00000000 *Var0 )
                Wait  1`
                If  *Var1  ==  00000000
                    BreakLoop
                EndIf
                If  *MapVar[1]  <=  00000000
                    Goto 0
                EndIf
            EndLoop
    Wait 30`
    Exec $Script_MissionComplete
    Return
    Label 0
    Call GetModelCenter ( ~Model:Enemy1 )
    Wait 10`
    Call  PlayEffect ( ~FX:SmokeBurst:Red *Var0 *Var1 *Var2 *Fixed[4.0] 0000000A 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )
    Wait 10`
    Call  PlayEffect ( ~FX:SmokeBurst:Red *Var0 *Var1 *Var2 *Fixed[5.0] 0000000A 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )
    Wait 10`
    Call  PlayEffect ( ~FX:SmokeBurst:Red *Var0 *Var1 *Var2 *Fixed[6.0] 0000000A 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )
    Call GetModelCenter ( ~Model:Enemy1 )
    Set *Var7 *Var0
    Set *Var5 1`
    Call  ModifyColliderFlags   ( 00000001 ~Collider:Enemy1Collider 7FFFFE00 )
    Call MakeLerp ( 1500` 2000` 60` .Easing:CosIn )
            Loop
                Call  UpdateLerp ( )
                Call  TranslateModel ( ~Model:Enemy1 *Var7 00000000 *Var0 )
                Call  UpdateColliderTransform ( ~Collider:Enemy1Collider )
                Call  RotateGroup ( ~Model:EnemyShip1 *Var5 00000000 *Var5 00000000 )
                Add *Var5 1`
                Wait  1`
                Set *Debug[0] *Var7
                If  *Var1  ==  00000000
                    BreakLoop
                EndIf
            EndLoop
        Call GetModelCenter ( ~Model:Enemy1 )
        Call  PlaySoundAtModel ( ~Model:Enemy1 .Sound:Cannon2 00000000 )
        Call  PlayEffect ( ~FX:Explosion3 *Var0 *Var1 *Var2 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )
        Call EnableModel ( ~Model:Enemy1 .False )
        Call  ModifyColliderFlags   ( 00000000 ~Collider:Enemy1Collider 7FFFFE00 )
        Add *MB_EnemiesDestroyed 5`
        Wait 30`
        Exec $Script_MissionComplete
    Return
    End
}

#new:Script $Script_FiringSequence
{
    Thread
        Loop   
            Call PlaySoundAtModel ( ~Model:Enemy1 .Sound:BombBlast 00000000 )
            Call GetModelCenter ( ~Model:Enemy1 )
            Add *Var2 15`  
            Call TranslateGroup ( ~Model:EnemyBullets *Var0 *Var1 *Var2 )
            Call EnableGroup ( ~Model:EnemyBullets .True )
            Call UpdateColliderTransform ( ~Collider:BulletCollider1 )
            Call  ModifyColliderFlags   ( 00000001 ~Collider:BulletCollider1 7FFFFE00 )
            Call UpdateColliderTransform ( ~Collider:BulletCollider2 )
            Call  ModifyColliderFlags   ( 00000001 ~Collider:BulletCollider2 7FFFFE00 )
            Call UpdateColliderTransform ( ~Collider:BulletCollider3 )
            Call  ModifyColliderFlags   ( 00000001 ~Collider:BulletCollider3 7FFFFE00 )
            Exec $Script_BulletsMove
            Wait 60`
            If *MapVar[1] <= 0`
                BreakLoop
            EndIf
            If *MapFlag[3] == .True
                BreakLoop
            EndIf
        EndLoop
    EndThread
    Return
    End
}

#new:Script $Script_BulletsMove
{
    Thread
        Call GetModelCenter ( ~Model:Bullet1 )
        Set *Var3 *Var2
        Set *Var4 *Var0
        Call MakeLerp ( *Var3 250` 45` .Easing:Linear )
            Loop
                Call  UpdateLerp ( )
                Call  TranslateModel ( ~Model:Bullet1 *Var4 00000000 *Var0 )
                Call  UpdateColliderTransform ( ~Collider:BulletCollider1 )
                Wait  1`
                If  *Var1  ==  00000000
                    BreakLoop
                EndIf
            EndLoop
        Call EnableModel ( ~Model:Bullet1 .False )
        Call  ModifyColliderFlags   ( 00000000 ~Collider:BulletCollider1 7FFFFE00 )
    EndThread
    Thread
        Call GetModelCenter ( ~Model:Bullet2 )
        Set *Var3 *Var2
        Set *Var5 *Var0
        Call MakeLerp ( *Var3 250` 45` .Easing:Linear )
            Loop
                Call  UpdateLerp ( )
                Call  TranslateModel ( ~Model:Bullet2 *Var5 00000000 *Var0 )
                Call  UpdateColliderTransform ( ~Collider:BulletCollider2 )
                Add *Var5 3`
                Wait  1`
                If  *Var1  ==  00000000
                    BreakLoop
                EndIf
            EndLoop
        Call EnableModel ( ~Model:Bullet2 .False )
        Call  ModifyColliderFlags   ( 00000000 ~Collider:BulletCollider2 7FFFFE00 )
    EndThread
    Thread
        Call GetModelCenter ( ~Model:Bullet3 )
        Set *Var3 *Var2
        Set *Var6 *Var0
        Call MakeLerp ( *Var3 250` 45` .Easing:Linear )
            Loop
                Call  UpdateLerp ( )
                Call  TranslateModel ( ~Model:Bullet3 *Var6 00000000 *Var0 )
                Call  UpdateColliderTransform ( ~Collider:BulletCollider3 )
                Sub *Var6 3`
                Wait  1`
                If  *Var1  ==  00000000
                    BreakLoop
                EndIf
            EndLoop
        Call EnableModel ( ~Model:Bullet3 .False )
        Call  ModifyColliderFlags   ( 00000000 ~Collider:BulletCollider3 7FFFFE00 )
    EndThread
    Return
    End
}

#new:Script $Script_BulletBlowUp1
{
    Call  ModifyColliderFlags   ( 00000000 ~Collider:BulletCollider1 7FFFFE00 )
    Call  PlaySoundAtModel ( ~Model:Bullet1 .Sound:HitBlock 00000000 )
    Call  GetModelCenter ( ~Model:Bullet1 )
    Call  PlayEffect ( ~FX:ShockWave:LargeWhite *Var0 *Var1 *Var2 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )
    Call  EnableModel ( ~Model:Bullet1 .False )
    Call  $Function_DamageShip (  )
    Wait 1`
    Exec  $Script_CheckDamage
    Return
    End
}

#new:Script $Script_BulletBlowUp2
{
    Call  ModifyColliderFlags   ( 00000000 ~Collider:BulletCollider2 7FFFFE00 )
    Call  PlaySoundAtModel ( ~Model:Bullet2 .Sound:HitBlock 00000000 )
    Call  GetModelCenter ( ~Model:Bullet2 )
    Call  PlayEffect ( ~FX:ShockWave:LargeWhite *Var0 *Var1 *Var2 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )
    Call  EnableModel ( ~Model:Bullet2 .False )
    Call  $Function_DamageShip (  )
    Wait 1`
    Exec  $Script_CheckDamage
    Return
    End
}

#new:Script $Script_BulletBlowUp3
{
    Call  ModifyColliderFlags   ( 00000000 ~Collider:BulletCollider3 7FFFFE00 )
    Call  PlaySoundAtModel ( ~Model:Bullet3 .Sound:HitBlock 00000000 )
    Call  GetModelCenter ( ~Model:Bullet3 )
    Call  PlayEffect ( ~FX:ShockWave:LargeWhite *Var0 *Var1 *Var2 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )
    Call  EnableModel ( ~Model:Bullet3 .False )
    Call  $Function_DamageShip (  )
    Wait 1`
    Exec  $Script_CheckDamage
    Return
    End
}

#new:Script $Script_Enemy1Ram
{
    Call  PlaySoundAtModel ( ~Model:Enemy1 .Sound:HitBlock 00000000 )
    Call  $Function_DamageShip (  )
    Wait 1`
    Exec  $Script_CheckDamage
    Wait 10`
    Return
    End
}

#new:Script $Script_CheckDamage
{
    Set *MF_SpacePerfectBonus .False
    Call $Function_GetArbitraryByte ( *Var0 8010F292 )
    If *Var0 == 1`
        Call DisablePlayerInput ( .True )
        Set *MF_SpaceFailState .True
        Call  ShowMessageAtScreenPos    ( 002F005D  160`  40` ) % We gotta get outta here!
        Set *MB_SpaceCoins 0`
        Set *MB_EnemiesDestroyed 0`
        Call GotoMap ( "sar_00" ~Entry:sar_00:GameStart )
    EndIf
    Return
    End
}

#new:Function $Function_DamageShip
{
    LA        v0, 8010F290 %Load Address of current HP
    LBU       t0, 2 (v0) %Load value of the address, current HP
    ADDIU     t0, t0, -1 %Subtract 1 from current HP, apply value to t0
    ADDIU      t1, t0, -1 %Subtract 1 from t0, apply to t1
    BLTZ      t1, .o31 %Jump to .031 if t1 is less than 0 (Does nothing if HP was 1)
    NOP                 %Extra space so the if statement can work
    SB        t0, 2 (v0) %Set the current HP value to t0
    .o31 %Label!
    JR RA
    ORI v0, r0, 2
}

#new:Script $Script_WeaponAttack
{
    If *MapFlag[1] == .True
        Return
    EndIf
    Set *MapFlag[1] .True
    Set *MapFlag[2] .False
    Call GetPlayerPos ( *Var0 *Var1 *Var2 )
    Sub *Var2 15`
    Set *MapVar[0] *Var0
    Switch *MB_CurrentShip
    Case == 5`
        Call RandInt ( 2` *Var0 )
        Switch *Var0
        Case == 0`
            Call EnableModel ( ~Model:ShipBullet6 .True )
            Call MakeLerp ( *Var2 -1000` 30` .Easing:Linear )
                Loop
                    Call  UpdateLerp ( )
                    Call  TranslateModel ( ~Model:ShipBullet6 *MapVar[0] 00000000 *Var0 )
                    Call RandInt ( 45` *Var2 )
                    Sub *Var2 23`
                    Add *MapVar[0] *Var2
                    Wait  1`
                    If  *Var1  ==  00000000
                        BreakLoop
                    EndIf
                    If  *MapFlag[2]  ==  .True
                        Set *MapFlag[2] .False
                        BreakLoop
                    EndIf
                EndLoop
            Call  TranslateModel ( ~Model:ShipBullet6 00000000 00000000 00000000 )
            Call  EnableModel ( ~Model:ShipBullet6 .False )
        Case == 1`
            Call EnableModel ( ~Model:ShipBullet7 .True )
            Call MakeLerp ( *Var2 -1000` 120` .Easing:CubicOut )
            SetF *Var3 *Fixed[0.1]
                Loop
                    Call  UpdateLerp ( )
                    Call  TranslateModel ( ~Model:ShipBullet7 *MapVar[0] 00000000 *Var0 )
                    AddF *Var3 *Fixed[0.1]
                    Call  ScaleModel ( ~Model:ShipBullet7 *Var3 *Var3 *Var3 )
                    Wait  1`
                    If  *Var1  ==  00000000
                        BreakLoop
                    EndIf
                    If  *MapFlag[2]  ==  .True
                        Set *MapFlag[2] .False
                        BreakLoop
                    EndIf
                EndLoop
            Call  TranslateModel ( ~Model:ShipBullet7 00000000 00000000 00000000 )
            Call  EnableModel ( ~Model:ShipBullet7 .False )
        Case == 2`
            Call EnableModel ( ~Model:ShipBullet8 .True )
            Call MakeLerp ( *Var2 -1000` 45` .Easing:Linear )
                Loop
                    Call  UpdateLerp ( )
                    Call  TranslateModel ( ~Model:ShipBullet8 *MapVar[0] 00000000 *Var0 )
                    Wait  1`
                    If  *Var1  ==  00000000
                        BreakLoop
                    EndIf
                    If  *MapFlag[2]  ==  .True
                        Set *MapFlag[2] .False
                        BreakLoop
                    EndIf
                EndLoop
            Call  TranslateModel ( ~Model:ShipBullet8 00000000 00000000 00000000 )
            Call  EnableModel ( ~Model:ShipBullet8 .False )
        EndSwitch
    Case == 4`
        Set *MapFlag[4] .False
        Set *MapFlag[5] .False
        Call EnableModel ( ~Model:ShipBullet3 .True )
        Call EnableModel ( ~Model:ShipBullet4 .True )
        Call EnableModel ( ~Model:ShipBullet5 .True )
        Thread
            Call MakeLerp ( *Var2 -1000` 20` .Easing:Linear )
            Loop
                Call  UpdateLerp ( )
                Call  TranslateModel ( ~Model:ShipBullet3 *MapVar[0] 00000000 *Var0 )
                Wait  1`
                If  *Var1  ==  00000000
                    BreakLoop
                EndIf
                If  *MapFlag[2]  ==  .True
                    Set *MapFlag[2] .False
                    BreakLoop
                EndIf
            EndLoop
            Call  TranslateModel ( ~Model:ShipBullet3 00000000 00000000 00000000 )
            Call  EnableModel ( ~Model:ShipBullet3 .False )
        EndThread
        Thread
            Call MakeLerp ( *Var2 -900` 20` .Easing:Linear )
            Set *Var3 *MapVar[0]
            Loop
                Call  UpdateLerp ( )
                Call  TranslateModel ( ~Model:ShipBullet4 *Var3 00000000 *Var0 )
                Add *Var3 7`
                Wait  1`
                If  *Var1  ==  00000000
                    BreakLoop
                EndIf
                If  *MapFlag[4]  ==  .True
                    Set *MapFlag[4] .False
                    BreakLoop
                EndIf
            EndLoop
            Call  TranslateModel ( ~Model:ShipBullet4 00000000 00000000 00000000 )
            Call  EnableModel ( ~Model:ShipBullet4 .False )
        EndThread
        Thread
            Call MakeLerp ( *Var2 -900` 20` .Easing:Linear )
            Set *Var4 *MapVar[0]
            Loop
                Call  UpdateLerp ( )
                Call  TranslateModel ( ~Model:ShipBullet5 *Var4 00000000 *Var0 )
                Sub *Var4 7`
                Wait  1`
                If  *Var1  ==  00000000
                    BreakLoop
                EndIf
                If  *MapFlag[5]  ==  .True
                    Set *MapFlag[5] .False
                    BreakLoop
                EndIf
            EndLoop
            Call  TranslateModel ( ~Model:ShipBullet5 00000000 00000000 00000000 )
            Call  EnableModel ( ~Model:ShipBullet5 .False )
        EndThread
        Wait 20`
    Case == 3`
        Call  PlaySoundAtPlayer ( 00002069 00000000 )
        Call  PlayEffect        ( ~FX:Lightning *Var0 0` *Var2 *Var0 0` -1000` *Fixed[10.0] 20` 00000000 00000000 00000000 00000000 )
        Wait 20`
        Set *MapFlag[1] .False
        Wait 30`
    Case == 2`
        Call EnableModel ( ~Model:ShipBullet2 .True )
        Call MakeLerp ( *Var2 -1000` 75` .Easing:CosIn )
            Loop
                Call  UpdateLerp ( )
                Call  TranslateModel ( ~Model:ShipBullet2 *MapVar[0] 00000000 *Var0 )
                Wait  1`
                If  *Var1  ==  00000000
                    BreakLoop
                EndIf
                If  *MapFlag[2]  ==  .True
                    Set *MapFlag[2] .False
                    BreakLoop
                EndIf
            EndLoop
        Call  TranslateModel ( ~Model:ShipBullet2 00000000 00000000 00000000 )
        Call  EnableModel ( ~Model:ShipBullet2 .False )
    Default
        Call EnableModel ( ~Model:ShipBullet1 .True )
        Call MakeLerp ( *Var2 -1000` 30` .Easing:Linear )
            Loop
                Call  UpdateLerp ( )
                Call  TranslateModel ( ~Model:ShipBullet1 *MapVar[0] 00000000 *Var0 )
                Wait  1`
                If  *Var1  ==  00000000
                    BreakLoop
                EndIf
                If  *MapFlag[2]  ==  .True
                    Set *MapFlag[2] .False
                    BreakLoop
                EndIf
            EndLoop
        Call  TranslateModel ( ~Model:ShipBullet1 00000000 00000000 00000000 )
        Call  EnableModel ( ~Model:ShipBullet1 .False )
    EndSwitch
    Set *MapFlag[1] .False
    Return
    End
}

#new:Script $Script_EnemyDamage
{
    Switch *MB_CurrentShip
    Case == 5`
        Loop
            Call GetModelCenter ( ~Model:Enemy1 )
            Set *Var3 *Var0
            Set *Var4 *Var2
            Call GetModelCenter ( ~Model:ShipBullet6 )
            Call GetDist2D ( *Var5 *Var0 *Var2 *Var3 *Var4 )
            If *Var5 <= 200`
                Call  PlaySoundAtModel ( ~Model:ShipBullet5 .Sound:HitBlock 00000000 )
                Goto 1    
            EndIf
            Call GetModelCenter ( ~Model:ShipBullet7 )
            Call GetDist2D ( *Var5 *Var0 *Var2 *Var3 *Var4 )
            If *Var5 <= 200`
                Call  PlaySoundAtModel ( ~Model:ShipBullet6 .Sound:HitBlock 00000000 )
                Goto 1    
            EndIf
            Call GetModelCenter ( ~Model:ShipBullet8 )
            Call GetDist2D ( *Var5 *Var0 *Var2 *Var3 *Var4 )
            If *Var5 <= 200`
                Call  PlaySoundAtModel ( ~Model:ShipBullet7 .Sound:HitBlock 00000000 )
                Goto 1    
            EndIf
            Wait 1`
            Goto 2
            Label 1
            Set *MapFlag[2] .True
            Wait 2`
            Call  PlayEffect ( ~FX:Explosion1 *Var0 *Var1 *Var2 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )
            Sub *MapVar[1] 2`
            Set *Debug[2] *MapVar[1]
            If *MapVar[1] <= 0`
                BreakLoop
            EndIf
            Label 2
            Wait 1`
        EndLoop
    Case == 4`
        Loop
            Call GetModelCenter ( ~Model:Enemy1 )
            Set *Var3 *Var0
            Set *Var4 *Var2
            Call GetModelCenter ( ~Model:ShipBullet3 )
            Call GetDist2D ( *Var5 *Var0 *Var2 *Var3 *Var4 )
            If *Var5 <= 200`
                Call  PlaySoundAtModel ( ~Model:ShipBullet3 .Sound:HitBlock 00000000 )
                Set *MapFlag[2] .True
                Goto 1    
            EndIf
            Call GetModelCenter ( ~Model:ShipBullet4 )
            Call GetDist2D ( *Var5 *Var0 *Var2 *Var3 *Var4 )
            If *Var5 <= 200`
                Call  PlaySoundAtModel ( ~Model:ShipBullet4 .Sound:HitBlock 00000000 )
                Set *MapFlag[4] .True
                Goto 1    
            EndIf
            Call GetModelCenter ( ~Model:ShipBullet5 )
            Call GetDist2D ( *Var5 *Var0 *Var2 *Var3 *Var4 )
            If *Var5 <= 200`
                Call  PlaySoundAtModel ( ~Model:ShipBullet5 .Sound:HitBlock 00000000 )
                Set *MapFlag[5] .True
                Goto 1    
            EndIf
            Wait 1`
            Goto 2
            Label 1
            Wait 2`
            Call  PlayEffect ( ~FX:Explosion1 *Var0 *Var1 *Var2 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )
            Sub *MapVar[1] 1`
            Set *Debug[2] *MapVar[1]
            If *MapVar[1] <= 0`
                BreakLoop
            EndIf
            Label 2
            Wait 1`
        EndLoop
    Case == 3`
        Loop 
            If *MapFlag[1] == .True
               Call GetModelCenter ( ~Model:Enemy1 )
                Call GetPlayerPos ( *Var1 00000000 00000000 )
                Sub *Var0 *Var1
                If *Var0 < 0`
                    Mul *Var0 -1`
                EndIf
                Set *Debug[1] *Var0
                If *Var0 <= 150`
                    Call GetModelCenter ( ~Model:Enemy1 )
                    Call  PlayEffect ( ~FX:Firework:Yellow *Var0 *Var1 *Var2 *Fixed[2.0] 30` 00000000 00000000 00000000 00000000 00000000 00000000 )
                    Sub *MapVar[1] 2`
                    Set *Debug[2] *MapVar[1]
                    Wait 50`
                EndIf
                If *MapVar[1] <= 0`
                    BreakLoop
                EndIf
            EndIf
            Wait 1`
        EndLoop
    Case == 2`
        Loop
            Call GetModelCenter ( ~Model:Enemy1 )
            Set *Var3 *Var0
            Set *Var4 *Var2
            Call GetModelCenter ( ~Model:ShipBullet2 )
            Call GetDist2D ( *Var5 *Var0 *Var2 *Var3 *Var4 )
            If *Var5 <= 200`
                Call  PlaySoundAtModel ( ~Model:ShipBullet2 .Sound:Cannon1 00000000 )
                Call  PlayEffect ( ~FX:Explosion1 *Var0 *Var1 *Var2 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )
                Sub *MapVar[1] 3`
                Set *Debug[2] *MapVar[1]
                Set *MapFlag[2] .True
                Wait 10`
            EndIf
            If *MapVar[1] <= 0`
                BreakLoop
            EndIf
            Wait 1`
        EndLoop
    Default
        Loop
            Call GetModelCenter ( ~Model:Enemy1 )
            Set *Var3 *Var0
            Set *Var4 *Var2
            Call GetModelCenter ( ~Model:ShipBullet1 )
            Call GetDist2D ( *Var5 *Var0 *Var2 *Var3 *Var4 )
            If *Var5 <= 200`
                Call  PlaySoundAtModel ( ~Model:ShipBullet1 .Sound:HitBlock 00000000 )
                Call  PlayEffect ( ~FX:Explosion1 *Var0 *Var1 *Var2 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )
                Sub *MapVar[1] 2`
                Set *Debug[2] *MapVar[1]
                Set *MapFlag[2] .True
                Wait 10`
            EndIf
            If *MapVar[1] <= 0`
                BreakLoop
            EndIf
            Wait 1`
        EndLoop
    EndSwitch
    Return
    End
}

#new:Script $Script_MissionComplete
{
    If *MF_SpaceFailState == .True
        Return
    EndIf
    Call DisablePlayerInput ( .True )
    Call SetMessageValue ( *MB_SpaceCoins 00000000 )
    Call SetMessageValue ( *MB_EnemiesDestroyed 00000001 )
    Set *Var0 = ( *MB_SpaceCoins + *MB_EnemiesDestroyed )
    If *MF_SpacePerfectBonus == .True
        Call SetMessageValue ( 10` 00000002 )
        Add *Var0 10`
    Else
        Call SetMessageValue ( 0` 00000002 )
    EndIf
    Set *Var1 *Var0
    Call SetMessageValue ( *Var0 00000003 )
    Call  ShowMessageAtScreenPos    ( 002F005E  160`  40` ) % Mission Complete!
    Call  AddCoin ( *Var1 )
    Wait 60`
    Set *MB_SpaceCoins 0`
    Set *MB_EnemiesDestroyed 0`
    Call GotoMap ( "sar_00" ~Entry:sar_00:GameStart )
    Return
    End
}

/% NOTES

MapFlag[0] determines whether ship tracking is active, and prevents it from stacking. This should always be on.
MapFlag[1] determines whether a friendly bullet is in flight, preventing it from stacking.
MapFlag[2] determines whether a friendly bullet has hit anything, resetting it.
MapFlag[3] determined whether the enemy's battle script is over, causing it to stop firing.
MapFlag[4] and MapFlag[5] are as MapFlag[2], for the Shooting Star's two additional bullets.

MapVar[0] is used to keep friendly bullets moving in a straight line.
MapVar[1] tracks the enemy ship's HP. If below 1, it stops firing, stops checking for collision, and enters its crashing sequence.
